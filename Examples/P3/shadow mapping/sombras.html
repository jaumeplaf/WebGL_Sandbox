<!DOCTYPE html>
<html>

  <head>
  
    <meta charset="utf-8">
    
    <title> Sombras </title>
    
    <style>
      canvas {border: 1px solid black; background: black;}
    </style>
    
    <script id="myVertexShaderToFBO" 
          type="x-shader/x-vertex">#version 300 es
      
      uniform   mat4 projectionMatrix;
      uniform   mat4 modelViewMatrix;
        
      in vec3 VertexPosition;
      
      void main()  {
          
        gl_Position = projectionMatrix * modelViewMatrix * vec4(VertexPosition,1.0);
          
      }
      
    </script>
    
    <script id="myFragmentShaderToFBO" 
          type="x-shader/x-fragment">#version 300 es
        
      precision lowp float;
           
      out vec4 fragmentColor;
      
      void main() {
        
        // these lines are not necessary
//         gl_FragDepth = gl_FragCoord.z;
//         fragmentColor = vec4(vec3(gl_FragDepth), 1.0);

      }
      
    </script>
      
    <script id="myVertexShader" 
          type="x-shader/x-vertex">#version 300 es
      
      uniform   mat4 projectionMatrix;
      uniform   mat4 modelViewMatrix;
      uniform   mat3 normalMatrix;
      
      uniform   mat4 projectionModelViewMatrixLight;
        
      in vec3 VertexPosition, VertexNormal;
      in vec2 VertexTexcoords;
        
      out vec3 N, ec;
      out vec2 texCoords;
      
      out vec4 posFromLight;
        
      void main()  {
          
        N               = normalize(normalMatrix * VertexNormal);
        vec4 ecPosition = modelViewMatrix * vec4(VertexPosition,1.0);
        ec              = vec3(ecPosition);
          
        texCoords       = VertexTexcoords;
        
        posFromLight    = projectionModelViewMatrixLight * vec4(VertexPosition,1.0);
        posFromLight    = (posFromLight / posFromLight.w) * 0.5 + 0.5;

        gl_Position     = projectionMatrix * modelViewMatrix * vec4(VertexPosition,1.0);
          
      }
      
    </script>
    
    <script id="myFragmentShader" 
          type="x-shader/x-fragment">#version 300 es
        
      precision lowp float;
        
      struct LightData {
        vec3 Position; // PosiciÃ³n en coordenadas del ojo
        vec3 La;       // Ambiente
        vec3 Ld;       // Difusa
        vec3 Ls;       // Especular
      };
      uniform LightData Light;
      
      struct MaterialData {
        vec3 Ka;       // Ambiente
        vec3 Kd;       // Difusa
        vec3 Ks;       // Especular
        float alpha;   // Brillo
      };
      uniform MaterialData Material;
      
      uniform highp sampler2DShadow shadowMap;
      
      uniform int tablero;  
      
      in  vec3 N, ec;
      in  vec2 texCoords;
      in  vec4 posFromLight;
      
      out vec4 fragmentColor;
      
      vec3 phong (vec3 N, vec3 L, vec3 V) {
        
        vec3  ambient  = Material.Ka * Light.La;
        vec3  diffuse  = vec3(0.0);
        vec3  specular = vec3(0.0);
        
        float NdotL    = dot (N,L);
        
        if (NdotL > 0.0) {
          vec3  R       = reflect(-L, N);
          float RdotV_n = pow(max(0.0, dot(R,V)), Material.alpha);
          
          diffuse  = NdotL   * (Light.Ld * Material.Kd);
          specular = RdotV_n * (Light.Ls * Material.Ks);
        }
        
        return (ambient + diffuse + specular);
          
      }
      
      float isBlue () {
          
        float size = 3.0;
        float res  = float(mod(floor(texCoords.s * size) +
                               floor(texCoords.t * size), 2.0) == 0.0);
          
        return (0.4 * res);
      
      }
      
      void main() {

        float visibility = textureProj(shadowMap, posFromLight) * 0.8 + 0.2;

        vec3 n = normalize(N);
        vec3 L = normalize(Light.Position - ec);
        vec3 V = normalize(-ec);
        
        vec4 color = vec4(phong(n,L,V), 1.0);
        color.rg -= (isBlue() * float(tablero));
        color.rg  = clamp (color.rg, 0.0, 1.0);
        
        fragmentColor = vec4(pow (vec3(color) * visibility, vec3 (1.0 / 2.2)), 1.0);
        
      }
      
    </script>

  </head>
  
  <body>
  
    <canvas id="myCanvas" style="border: none;" width="600" height="600">
      El Navegador no soporta HTML5
    </canvas>
      
    <script src="gl-matrix-min.js"></script>
    <script src="materiales.js"></script>
    <script src="primitivasGNT.js"></script>
    <script src="comun.js"></script>
    <script src="sombras.js"></script>
      
  </body>

</html>
