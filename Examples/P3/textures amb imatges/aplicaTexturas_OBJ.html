<!DOCTYPE html>
<html>

  <head>
  
    <meta charset="utf-8">
    
    <title> Aplica texturas </title>
    
    <style>
      canvas {border: 1px solid black;}
    </style>
    
    <script id="myVertexShader" 
          type="x-shader/x-vertex">#version 300 es
        
      uniform mat4 projectionMatrix;
      uniform mat4 modelViewMatrix;
      uniform mat3 normalMatrix;
        
      in vec3 VertexPosition;
      in vec3 VertexNormal;
      in vec2 VertexTexcoords;
        
      out vec3 N, ec;
      out vec2 texCoords;
        
      void main()  {
          
        N               = normalize(normalMatrix * VertexNormal);
        vec4 ecPosition = modelViewMatrix * vec4(VertexPosition,1.0);
        ec              = vec3(ecPosition);
          
        gl_Position     = projectionMatrix * ecPosition;
        texCoords       = VertexTexcoords;
        
      }
      
    </script>
      
    <script id="myFragmentShader" 
          type="x-shader/x-fragment">#version 300 es
        
      precision mediump float;
      
      struct LightData {
        vec3 Position; // PosiciÃ³n en coordenadas del ojo
        vec3 La;       // Ambiente
        vec3 Ld;       // Difusa
        vec3 Ls;       // Especular
      };
      uniform LightData Light;
      
      struct MaterialData {
        vec3 Ka;       // Ambiente
        vec3 Kd;       // Difusa
        vec3 Ks;       // Especular
        float alpha;   // Brillo
      };
      uniform MaterialData Material;
      
      uniform sampler2D myTexture;
      uniform float     repetition;
        
      in  vec3 N, ec;
      in  vec2 texCoords;
      out vec4 fragmentColor;
      
      vec3 phong (vec3 N, vec3 L, vec3 V) {
        
        vec3  ambient  = Material.Ka * Light.La;
        vec3  diffuse  = vec3(0.0);
        vec3  specular = vec3(0.0);
        
        float NdotL    = dot (N,L);
        
        if (NdotL > 0.0) {
          vec3  R       = reflect(-L, N);;
          float RdotV_n = pow(max(0.0, dot(R,V)), Material.alpha);
          
          diffuse  = NdotL   * (Light.Ld * Material.Kd);
          specular = RdotV_n * (Light.Ls * Material.Ks);
        }
        
        return (ambient + diffuse + specular);
        
      }
      
      void main() {
        
        vec3 n = normalize(N);
        vec3 L = normalize(Light.Position - ec);
        vec3 V = normalize(-ec);
        
        fragmentColor = texture(myTexture, texCoords * repetition) * vec4(phong(n,L,V), 1.0) * 1.6;
        
      }
      
    </script>
    
  </head>
  
  <body>
    
    La: <input type="color" name="La" value="#ffffff">
    Ld: <input type="color" name="Ld" value="#ffffff">
    Ls: <input type="color" name="Ls" value="#ffffff">
    <br>    
    Textura <strong>OBJ</strong>: <input type="file"  name="TextureFilename" accept="image/*">
    <br>
    Repetition:   <input type="range" name="Repetition" min="1" max="10" value="1">
    <br>
     
    <canvas id="myCanvas" width="600" height="600">
      El Navegador no soporta HTML5
    </canvas>
      
    <script src="gl-matrix-min.js"></script>
    <script src="materiales.js"></script>
    <script src="primitivasGNT.js"></script>
	<script src="suzzane.json"></script>
    <script src="aplicaTexturas_OBJ.js"></script>
      
  </body>
  
</html>
