<!DOCTYPE html>
<html>

  <head>

    <meta charset="utf-8">
    
    <title> Enrejado </title>
    
    <style type="text/css">
      canvas {border: 1px solid black; background:black}
    </style>
      
    <script id="myVertexShader" 
          type="x-shader/x-vertex">#version 300 es
      
      uniform   mat4 projectionMatrix;
      uniform   mat4 modelViewMatrix;
      uniform   mat3 normalMatrix;
        
      in vec3 VertexPosition;
      in vec3 VertexNormal;
      in vec2 VertexTexcoords;
        
      out vec3 N, ec;
      out vec2 texCoord;
        
      void main()  {
         
        N  = normalize(normalMatrix * VertexNormal);
        vec4 ecPosition = modelViewMatrix * vec4(VertexPosition,1.0);
        ec = vec3(ecPosition);
          
        gl_Position = projectionMatrix * ecPosition;
        texCoord    = VertexTexcoords;
        
      }
      
    </script>
    
    <script id="myFragmentShader" 
          type="x-shader/x-fragment">#version 300 es
      
      precision mediump float;

      struct LightData {
        vec3 Position; // Posición en coordenadas del ojo
        vec3 La;       // Ambiente
        vec3 Ld;       // Difusa
        vec3 Ls;       // Especular
      };
      uniform LightData Light;
     
      struct MaterialData {
        vec3 Ka;       // Ambiente
        vec3 Kd;       // Difusa
        vec3 Ks;       // Especular
        float alpha;   // Brillo
      };
      uniform MaterialData Material;
      
      uniform vec2 Scale;     // numero de agujeros
      uniform vec2 Threshold; // para el tamaño de los agujeros
        
      in vec3 N, ec;
      in vec2 texCoord;
      
      out vec4 fragmentColor;
      
      vec3 phong (vec3 N, vec3 L, vec3 V) {
        
        vec3  ambient  = Material.Ka * Light.La;
        vec3  diffuse  = vec3(0.0);
        vec3  specular = vec3(0.0);
        
        float NdotL    = dot (N,L);
        
        if (NdotL > 0.0) {
          vec3  R       = reflect(-L, N);;
          float RdotV_n = pow(max(0.0, dot(R,V)), Material.alpha);
          
          diffuse  = NdotL   * (Light.Ld * Material.Kd);
          specular = RdotV_n * (Light.Ls * Material.Ks);
        }
        
        return (ambient + diffuse + specular);
        
      }
      
      void main() {
          
        float ss = fract(texCoord.s * Scale.s);
        float tt = fract(texCoord.t * Scale.t);

        if ((ss > Threshold.s) && (tt > Threshold.t))
          discard;
        
        vec3 n = normalize(N);
        vec3 L = normalize(Light.Position - ec);
        vec3 V = normalize(-ec);
        
        fragmentColor = vec4( phong(n,L,V) * 1.4, 1.0);
          
      }
      
    </script>  
    
  </head> 

  <body>
  
    <button>Plano</button> 
    <button>Cubo</button> 
    <button>Tapa</button> 
    <button>Cono</button> 
    <button>Cilindro</button> 
    <button>Esfera</button> 
    <button>Toro</button> 
    <br>
    <input type="color" id="Kd"    name="color" value="#808080"> &nbsp;Kd<br>
    <input type="range" id="ScaleS" name="range" min="1" max="30" value="15"> &nbsp;Scale S<br>
    <input type="range" id="ScaleT" name="range" min="1" max="30" value="15"> &nbsp;Scale T<br>
    <input type="range" id="ThresholdS" name="range" min="1" max="100" value="50"> &nbsp;Threshold S<br>
    <input type="range" id="ThresholdT" name="range" min="1" max="100" value="50"> &nbsp;Threshold T<br>

    <canvas id="myCanvas" width="600" height="600">
      El Navegador no soporta HTML5
    </canvas>
      
    <script src="gl-matrix-min.js"></script>
    <script src="materiales.js"></script>
    <script src="primitivasGNT.js"></script>
    <script src="comun.js"></script>
    <script src="enrejado.js"></script>
      
  </body>
      
</html>
